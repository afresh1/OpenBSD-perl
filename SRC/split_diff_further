#!/usr/bin/perl
use strict;
use warnings;

my @header;
my @pre_header;
my $path;
my $fh;
while (<>) {

	# obviously will fail on filenames with spaces, but whatev
	if ( my ($first, $second) = /^diff.*?(\S+)\s+(\S+)$/ ) {
		$fh = undef;

		my @f = split m{/}, $first;
		my @s = split m{/}, $second;

		my @p;
		while ($f[-1] eq $s[-1]) {
			unshift @p, pop @f;
			pop @s;
		}

		$path = join '_', @p;
		@header = @pre_header = ();
	}

	if (/^\@\@ /) {
		$fh = undef if @header;
		@header = @pre_header if @pre_header;
		@pre_header = ();
	}

	if ($path and ! $fh) {
		my $file = 'patches/' . $path;
		my $ext = '';
		$ext++ while -e $file . $ext . '.patch';
		$file .= $ext . '.patch';
		print "$file\n";
		open $fh, '>', $file or die "Unable to open $path: $!";
		print $fh @header if @header;
	}

	push @pre_header, $_ unless @header;

	print $fh $_ if $fh;
}
